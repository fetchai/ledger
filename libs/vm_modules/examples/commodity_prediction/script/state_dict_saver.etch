function build_graph(graph: Graph)
// make graph with correct architecture
var dropout_prob = 1.0f;
var input_size = 118;
var first_dense_size = 216;
var second_dense_size = 108;
var output_size = 54;

graph.AddPlaceholder("num_input");
graph.AddDropout("dropout_0", "num_input", dropout_prob);
graph.AddFullyConnected("hidden_dense_1", "dropout_0", input_size, first_dense_size);
graph.AddSoftmax("softmax_1", "hidden_dense_1");
graph.AddDropout("dropout_1", "softmax_1", dropout_prob);
graph.AddFullyConnected("hidden_dense_2", "dropout_1", first_dense_size, second_dense_size);
graph.AddSoftmax("softmax_2", "hidden_dense_2");
graph.AddDropout("dropout_2", "softmax_2", dropout_prob);
graph.AddFullyConnected("output_dense", "dropout_2", second_dense_size, output_size);
graph.AddSoftmax("softmax_3", "output_dense");
endfunction

function read_weights(graph: Graph)
// read in weights
var file_weights0 = System.Argv(0) +  "/output/keras_aluminium_px_last_us/model_weights/hidden_dense_1/hidden_dense_1_12/kernel:0.csv";
var file_bias0    = System.Argv(0) +  "/output/keras_aluminium_px_last_us/model_weights/hidden_dense_1/hidden_dense_1_12/bias:0.csv";
var file_weights1 = System.Argv(0) +  "/output/keras_aluminium_px_last_us/model_weights/hidden_dense_2/hidden_dense_2_4/kernel:0.csv";
var file_bias1    = System.Argv(0) +  "/output/keras_aluminium_px_last_us/model_weights/hidden_dense_2/hidden_dense_2_4/bias:0.csv";
var file_weights2 = System.Argv(0) +  "/output/keras_aluminium_px_last_us/model_weights/output_dense/output_dense_12/kernel:0.csv";
var file_bias2    = System.Argv(0) +  "/output/keras_aluminium_px_last_us/model_weights/output_dense/output_dense_12/bias:0.csv";

var weights0 = read_csv(file_weights0, true);
var bias0    = read_csv(file_bias0, false);
var weights1 = read_csv(file_weights1, true);
var bias1    = read_csv(file_bias1, false);
var weights2 = read_csv(file_weights2, true);
var bias2    = read_csv(file_bias2, false);


// load weights into graph
var sd = graph.StateDict();
sd.SetWeights("hidden_dense_1" + "_FC_Weights", weights0);
sd.SetWeights("hidden_dense_1" + "_FC_Bias", bias0);
sd.SetWeights("hidden_dense_2" + "_FC_Weights", weights1);
sd.SetWeights("hidden_dense_2" + "_FC_Bias", bias1);
sd.SetWeights("output_dense" + "_FC_Weights", weights2);
sd.SetWeights("output_dense" + "_FC_Bias", bias2);

graph.LoadStateDict(sd);  // todo: currently unnecessary because sd is a reference to graph's state dict.
endfunction

function main()
// Currently saves state dict - eventually this will load and save to State the whole graph

if (System.Argc() < 1)
  printLn("Usage: VM SCRIPT_FILE PATH/TO/model_files/");
  return;
endif

var graph = Graph();
build_graph(graph);
read_weights(graph);


var sd = graph.StateDict();
var state = State<StateDict>("foo");
state.set(sd);
printLn("Saved sd to state");

endfunction
